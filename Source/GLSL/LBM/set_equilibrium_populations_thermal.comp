#<compute shader>

#version 460 core

#define D2Q9 0
#define D3Q15 1
#define D3Q19 2
#define D3Q27 3

#define fp16 0
#define fp32 1

#define floating_point_accuracy fp32
#define velocity_set D2Q9

#if velocity_set == D2Q9
	#define volume_dimentionality 2
	#define velocity_count 9
#elif velocity_set == D3Q15
	#define volume_dimentionality 3
	#define velocity_count 15
#elif velocity_set == D3Q19
	#define volume_dimentionality 3
	#define velocity_count 19
#elif velocity_set == D3Q27
	#define volume_dimentionality 3
	#define velocity_count 27
#else
	#error "velocity set is not supported"
#endif

#if floating_point_accuracy == fp32
#else
	#error "other floating point systems than fp32 aren't supported"
#endif

#define D2Q5 4
#define D3Q7 5

#define thermal_flow 1
#define velocity_set_thermal D2Q5

#if velocity_set_thermal == D2Q5
	#define thermal_velocity_count 5
#elif velocity_set_thermal == D3Q7
	#define thermal_velocity_count 7
#else
	#error "thermal_velocity_set is not supported"
#endif

#define multiphase_flow 1

#define id gl_GlobalInvocationID

layout (local_size_x = 64, local_size_y = 1, local_size_z = 1) in;

layout(std140, binding = 0) uniform velocity_set_buffer {
	vec4[thermal_velocity_count] lattice_velocity_set;
};

layout(std430, binding = 1) buffer lattice_buffer {
	float[] lattice;
};

#define velocity_field_dimentionality 4
layout(std430, binding = 2) buffer velocity_buffer {
	float[] velocities;
};

layout(std430, binding = 3) buffer temperature_buffer {
	float[] temperatures;
};

uniform ivec3 lattice_resolution;

int get_inverse_lattice_population_id_index(int lattice_velocity_index){
	if (lattice_velocity_index == 0 || lattice_velocity_index >= thermal_velocity_count)
		return 0;

	bool is_odd = lattice_velocity_index % 2 == 1;
	return is_odd ? lattice_velocity_index + 1 : lattice_velocity_index - 1;
}

float get_lattice(ivec2 pixel_coord, int velocity_index){
	return lattice[(pixel_coord.y * lattice_resolution.x + pixel_coord.x) * thermal_velocity_count + velocity_index];
}

void set_lattice(ivec2 pixel_coord, int velocity_index, float value){
	lattice[(pixel_coord.y * lattice_resolution.x + pixel_coord.x) * thermal_velocity_count + velocity_index] = value;
}

vec3 get_velocity_field(ivec2 pixel_coord){
	return vec3 (
		velocities[(pixel_coord.y * lattice_resolution.x + pixel_coord.x) * velocity_field_dimentionality + 0],
		velocities[(pixel_coord.y * lattice_resolution.x + pixel_coord.x) * velocity_field_dimentionality + 1],
		velocities[(pixel_coord.y * lattice_resolution.x + pixel_coord.x) * velocity_field_dimentionality + 2]
	);
}

void set_velocity_field(ivec2 pixel_coord, vec3 value){
	velocities[(pixel_coord.y * lattice_resolution.x + pixel_coord.x) * velocity_field_dimentionality + 0] = value.x;
	velocities[(pixel_coord.y * lattice_resolution.x + pixel_coord.x) * velocity_field_dimentionality + 1] = value.y;
	velocities[(pixel_coord.y * lattice_resolution.x + pixel_coord.x) * velocity_field_dimentionality + 2] = value.z;
}

float get_temperature_field(ivec2 pixel_coord){
	return temperatures[(pixel_coord.y * lattice_resolution.x + pixel_coord.x)];
}

void set_temperature_field(ivec2 pixel_coord, float value){
	temperatures[(pixel_coord.y * lattice_resolution.x + pixel_coord.x)] = value;
}

vec3 get_lattice_velocity(int velocity_index){
	return lattice_velocity_set[velocity_index].xyz;
}

float get_lattice_weight(int velocity_index){
	return lattice_velocity_set[velocity_index].w;
}

float compute_equilibrium(float temperature, vec3 velocity, int population_id){
	float T = temperature;
	vec3 u = velocity;
	float w = get_lattice_weight(population_id);
	vec3 ci = get_lattice_velocity(population_id);
	//float cs = lattice_speed_of_sound;

	float ci_dot_u = dot(ci, u);
	float ci_dot_u2 = pow(ci_dot_u, 2);
	float u_dot_u = dot(u, u);
	//float cs2 = pow(cs, 2);
	//float cs4 = pow(cs, 4);

	float equilibrium = w*T*(
		1 + 3*ci_dot_u + 9 * ci_dot_u2/2 - 3*u_dot_u/2
	); 
	return equilibrium;
}

void main(){
	
	if (id.x >= lattice_resolution.x * lattice_resolution.y)
		return;

	int pixel_id = int(id.x);
	ivec2 pixel_coord = ivec2(pixel_id % lattice_resolution.x, pixel_id / lattice_resolution.x);

	float temperature = get_temperature_field(pixel_coord);
	if (temperature == 0)
		return;

	vec3 velocity = get_velocity_field(pixel_coord);
	
	for (int population_id = 0; population_id < thermal_velocity_count; population_id++){
		float weight = get_lattice_weight(population_id);
		float equilibrium_population = compute_equilibrium(temperature, velocity, population_id);
		set_lattice(pixel_coord, population_id, equilibrium_population);
	}
}